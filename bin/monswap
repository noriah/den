#!/usr/bin/env zsh

INPUT_ADDR=0x60

# DDC/CI codes for inputs sources
INPUT_DISPLAYPORT1=15
INPUT_DISPLAYPORT2=16
INPUT_HDMI=17
INPUT_HDMI2=18

# serial numbers of monitors
MONITOR_CENTER=D2FPF43
MONITOR_LEFT=3XGX623
MONITOR_RIGHT=7SGX623

SLEEP_MULTIPLIER=0.01

DISPLAY_DATA_FILE="/tmp/vix_monitor_ids"

typeset -A display_alias=(
  [center]=$MONITOR_CENTER
  [left]=$MONITOR_LEFT
  [right]=$MONITOR_RIGHT
)

typeset -A input_alias=(
  [dp]=$INPUT_DISPLAYPORT1
  [dp2]=$INPUT_DISPLAYPORT2
  [hdmi]=$INPUT_HDMI
  [hdmi2]=$INPUT_HDMI2
)

swapinput() {
  if [ ! -f $DISPLAY_DATA_FILE ]; then
    echo $(ddcutil detect --sleep-multiplier $SLEEP_MULTIPLIER --dsa | awk -v RS= -F'\n' '
    BEGIN { print "typeset -A displays=(" }
    match($2, / +I2C bus:  \/dev\/i2c-([0-9]+)/, b) \
      match($8, / +Serial number: +(\w+)/, s) \
      { print "  [" s[1] "]=" b[1] }
    END { print ")" }') > $DISPLAY_DATA_FILE

  fi

  eval $(cat $DISPLAY_DATA_FILE)

  local _id=$display_alias[$1]
  local _dsp=$displays[$_id]
  local _inp=$input_alias[$2]

  local _bar_hook_idx
  case "$2" in
    dp|dp2)
      _bar_hook_idx=1
      ;;
    hdmi|hdmi2)
      _bar_hook_idx=2
      ;;
  esac

  ddcutil --sleep-multiplier $SLEEP_MULTIPLIER --dsa \
    --bus $_dsp setvcp $INPUT_ADDR $_inp && update_bar_icon $1 $_bar_hook_idx
}

update_bar_icon() {
  for i in $(pgrep -a "polybar" | cut -d" " -f1); do
    polybar-msg -p "$i" hook $1-monitor-control $2 1>/dev/null 2>&1
  done
}

swapinput $1 $2
